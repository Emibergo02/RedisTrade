plugins {
    id 'java'
    id 'io.github.goooler.shadow' version "8.1.7"
    id 'maven-publish'
}

group = 'dev.unnm3d'
version = '1.0.1'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven { url 'https://repo.xenondevs.xyz/releases' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT"
    compileOnly 'com.zaxxer:HikariCP:5.1.0'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    compileOnly 'io.lettuce:lettuce-core:6.4.0.RELEASE'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly "com.github.Emibergo02:RedisEconomy:4.3.19"

    implementation 'de.exlll:configlib-paper:4.5.0'
    implementation 'com.github.alexdev03:drink:1.5.3'
    implementation 'xyz.xenondevs.invui:invui:1.43'
    implementation 'com.github.Anon8281:UniversalScheduler:0.1.6'

    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation("org.junit.jupiter:junit-jupiter:5.10.2")
    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation 'io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.93.2'
    testImplementation 'com.zaxxer:HikariCP:5.1.0'
    testImplementation 'org.xerial:sqlite-jdbc:3.46.0.0'
    testImplementation 'org.projectlombok:lombok:1.18.34'
    testImplementation 'io.lettuce:lettuce-core:6.4.0.RELEASE'
    testImplementation "com.github.MilkBowl:VaultAPI:1.7.1"
    testImplementation "com.github.Emibergo02:RedisEconomy:4.3.19"

}

test {
    useJUnitPlatform()
}

shadowJar {
    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')

    relocate 'de.exlll.configlib', 'dev.unnm3d.redistrade.libraries.configlib'
    relocate 'dev.jorel.commandapi', 'dev.unnm3d.redistrade.libraries.commandapi'
    relocate 'com.jonahseguin.drink', 'dev.unnm3d.redistrade.libraries.drink'
    relocate 'xyz.xenondevs.invui', 'dev.unnm3d.redistrade.libraries.invui'
    relocate 'xyz.xenondevs.inventoryaccess', 'dev.unnm3d.redistrade.libraries.inventoryaccess'
    relocate 'com.github.Anon8281.universalScheduler', 'dev.unnm3d.redistrade.libraries.universalScheduler'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.unnm3d'
            artifactId = 'redistrade'
            version = "$version"
            artifact shadowJar
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
