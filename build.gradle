plugins {
    id 'java'
    id 'io.github.goooler.shadow' version "8.1.7"
}

group = 'dev.unnm3d'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT"
    compileOnly 'com.zaxxer:HikariCP:5.1.0'
    compileOnly 'org.projectlombok:lombok:1.18.34'

    implementation 'de.exlll:configlib-paper:4.5.0'
    implementation 'dev.jorel:commandapi-bukkit-shade:9.5.1'
    implementation 'com.github.alexdev03:drink:1.5.1'

    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

shadowJar {
    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')

    relocate 'de.exlll.configlib', 'dev.unnm3d.redistrade.libraries.configlib'
    relocate 'dev.jorel.commandapi', 'dev.unnm3d.redistrade.libraries.commandapi'
    relocate 'com.jonahseguin.drink', 'dev.unnm3d.redistrade.libraries.drink'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
